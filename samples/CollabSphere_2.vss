%REM
	Copyright 2022-2023 HCL America, Inc.
	Licensed under the Apache License, Version 2.0 (the "License"); 
	you may not use this file except in compliance with the License. 
	You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

	Unless required by applicable law or agreed to in writing, 
	software distributed under the License is distributed on an "AS IS" BASIS, 
	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
	See the License for the specific language governing permissions and limitations under the License	
%END REM

Option Public
Option Declare
Use "../src/VoltScriptJsonConverter"

Class Session
    Private sessiondate_ As String 
    Private sessiontime_ As String 
    Public unid As String 
    Public title As String
    Public room As String 
    Public startTime As String
    Public endTime As String

    Sub New(unid as String, title as String)
        Me.unid = unid
        Me.title = title 
    End Sub

    Property Get SessionDate As String 
        Dim chunks as Variant 

        If (Len(Me.sessiondate_) > 0) Then return me.sessiondate_
       
        chunks = split(me.startTime, |T|)
        if (ubound(chunks) > 0) Then me.sessiondate_ = cstr(chunks(0))

        return me.sessiondate_ 
    End Property 

    Property Get SessionTime As String 
        Dim chunks as Variant 
        Dim strTemp as String 

        If (Len(Me.sessiontime_) > 0) Then return me.sessiontime_
        
        If (Len(me.SessionDate) < 1) || (Len(Me.endTime) < 1) Then return

        chunks = split(me.startTime, |T|)
        strTemp = Left$(chunks(Ubound(chunks)), 5)

        chunks = split(me.endTime, |T|)
        chunks(0) = strTemp 
        strTemp = Left$(chunks(Ubound(chunks)), 5)
        chunks(1) = strTemp 
        Me.sessiontime_ = Join(chunks, | - |)

        return me.sessiontime_ 
    End Property 

    Sub printSummary()
        Print "Title:   " & Me.title 
        Print "Date:    " & Me.SessionDate
        Print "Time:    " & Me.SessionTime 
        Print "Room:    " & Me.room  
        Print "Unid:    " & Me.unid
    End Sub
End Class

Sub Initialize
    Dim job As JsonObject

    Dim parser As New JsonParser()
    Dim helper As New JsonConversionHelper()
    Dim sessionConstructor As New JsonCustomConstructor()
    Dim jscStart As New JsonScalarConverter()
    Dim jscEnd As New JsonScalarConverter()
    Dim ogs As Session

    Dim sessions As Variant
    Dim i As Integer


    ' BEGIN IMPLEMENTATION 
    Call parser.loadFromFile(CurDir() & "/samples/collabsphere.json") 
	' CurDir() as referenced above is set at RUN-TIME
    ' it represents the directory from which the script is BEING RUN; 
	' not the COMPILE-TIME directory in which that the script file resides.

    Set job = parser.getRootobject

    ' jscStart.forPropertyName and jscEnd.forPropertyName are both run before either withCustomConverter is run
    ' So if we use the same variable, both custom converters write to endTime
    sessions = helper.ignoreLabel("debug-log")._
        ignoreLabel("backgroundColor")._
        ignoreLabel("textColor")._
        ignoreLabel("className")._
        withCustomConstructor(sessionConstructor.withParam("id","").withParam("title", ""))._
        withCustomConverter("start", jscStart.forPropertyName("startTime"))._
        withCustomConverter("end", jscEnd.forPropertyName("endTime"))._
        jsonArrayToObjects(job, "Session", "CollabSphere_2")

    For i = Lbound(sessions) to Ubound(sessions)
        ' use unid variable
        If (|9F3F73226F22F82F862589EB0014CB89| = Cstr(sessions(i).unid)) Then
            Set ogs = sessions(i)
            Exit For
        End If
    Next

    If (ogs is Nothing) Then
        Print "We could not find the OGS of CollabSphere"
    Else
        Print "Found OGS of CollabSphere as session " & i
        Call ogs.printSummary()
    End If
End Sub