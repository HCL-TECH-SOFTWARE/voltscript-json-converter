%REM
	Copyright 2022-2023 HCL America, Inc.
	Licensed under the Apache License, Version 2.0 (the "License"); 
	you may not use this file except in compliance with the License. 
	You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

	Unless required by applicable law or agreed to in writing, 
	software distributed under the License is distributed on an "AS IS" BASIS, 
	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
	See the License for the specific language governing permissions and limitations under the License	
%END REM

Option Public
Option Declare
Use "../src/VoltScriptJsonConverter"


Class Prize

    Public year as String
    Public category as String
    Public overallMotivation as String
    Public laureates as Variant

End Class

Class Laureate

    Public id as String
    Public firstName as String
    Public surname as String
    Public motivation as String
    Public share as String

    Function getName as String
        getName = Me.firstName & " " & Me.surname
    End Function

End Class

Sub Initialize

    Dim parser as new JsonParser()
    Dim helper as New JsonConversionHelper
    Dim converter as New JsonBasicObjectArrayConverter("Laureate", "NobelPrizes")
    Dim root as JsonObject
    Dim prizes as Variant
    Dim prizeObj as JsonObject
    Dim prize as Prize
    Dim i as Integer
    Dim prizeList List as Prize

    ' Downloaded from https://api.nobelprize.org/v1/prize.json
    Call parser.loadFromFile(CurDir() & "/samples/nobel.json")
    Set root = parser.getRootobject
    prizes = root.getChild("prizes").getChildren()

    For i = 0 to UBound(prizes)
        Set prizeObj = prizes(i)
        Set prize = helper.withCustomConverter("laureates", converter)._
            toObject(prizeObj, "Prize", "NobelPrizes")._
            fromJson(prizeObj)
        Set prizeList(prize.year & "-" & prize.category) = prize
    Next

    Print prizeList("2021-economics").laureates(0).getName()

End Sub