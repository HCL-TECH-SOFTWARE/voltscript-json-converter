%REM
	Copyright 2022-2023 HCL America, Inc.
	Licensed under the Apache License, Version 2.0 (the "License"); 
	you may not use this file except in compliance with the License. 
	You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

	Unless required by applicable law or agreed to in writing, 
	software distributed under the License is distributed on an "AS IS" BASIS, 
	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
	See the License for the specific language governing permissions and limitations under the License	
%END REM

Option Public
Option Declare
Use "../src/VoltScriptJsonConverter"
Use "../libs/VoltScriptTesting"

Private Const RELATIVE_PATH_PREFIX = "../test/"

Class PersonProp

    Public firstName as String
    Public surname as String
    Public pets as Variant

    Function getName() as String
        getName = firstName & " " & surname
    End Function

End Class

Class PetProp

    Public name as String
    Public type as String

End Class

Class PetConverterProp as AbstractJsonConverter

    Function fromJson(source as JsonObject) as Variant

        Dim pets as Variant
        Dim helper as New JsonConversionHelper()
        pets = helper.jsonArrayToObjects(source, "PetProp", RELATIVE_PATH_PREFIX & "ComplexPersonPropertiesTest")
        fromJson = pets

    End Function

    Function toJson(source as Variant) as Variant
        Dim helper as New JsonConversionHelper()
        Dim jsonObj as JsonObject
        DebugPrint_Samsara "Converting pets from " & TypeName(source)
        Set jsonObj = helper.withScalarConverter("name").withScalarConverter("type").toJson(source)
        DebugPrint_Samsara "Converted pets"
        Set toJson = jsonObj
    End Function

End Class

Function runComplexPersonPropertiesTest() as Boolean

    Dim testRunner as New TestRunner("Complex Person Class Conversion to Custom Properties")
    Dim helper as new JsonConversionHelper()
    Dim petConverter as New PetConverterProp()
    Dim surnameConverter as New JsonScalarConverter()
    Dim person as PersonProp
    Dim json as String
    Dim result as Boolean
    Call testRunner.setOutputFormat("BOTH")
    
	json = |{"firstName":"Paul","lastName":"Withers","animals": [{"name":"Peter","type":"Guinea Pig"},{"name":"Benjamin","type":"Guinea Pig"}]}|
    'Call helper.enableDebug()
    
    Try
        Dim deserializeSuite as New TestSuite("Test Complex Person Class deserialization to Custom Properties")
        Call testRunner.addTestSuite(deserializeSuite)
        Call deserializeSuite.describe("Deserialization")
        Call helper.withCustomConverter("lastName", surnameConverter.forPropertyName("surname"))
        Call petConverter.forPropertyName("pets")
        Call helper.withCustomConverter("animals", petConverter)
        Set person = helper.fromJsonString(json, "PersonProp", RELATIVE_PATH_PREFIX & "ComplexPersonPropertiesTest")
        call deserializeSuite.addResult(True, "")
        Call testDeserializationPP(deserializeSuite, person)
    Catch
        Call deserializeSuite.addError("Error deserializing JSON: " & Error() & " on line " & Erl, ||)
        print "Error " & Error() & " on line " & Erl
    Finally
        result = deserializeSuite.ranSuccessfully()
    end Try

    Try
        Dim jsonObj as JsonObject
        Dim serializeSuite as New TestSuite("Test Person Class serialization")
        Call testRunner.addTestSuite(serializeSuite)
        Call serializeSuite.describe("Serialization")
        'Call helper.enableDebug()
        Call helper.withScalarConverter("firstName")
        Set jsonObj = helper.toJson(person)
        Call serializeSuite.addResult(True, "")
        Call testSerializationPP(serializeSuite, jsonObj)
    Catch
        Call serializeSuite.addError("Error deserializing JSON: " & Error() & " on line " & Erl, ||)
        print "Error " & Error() & " on line " & Erl
    Finally
        result = result And serializeSuite.ranSuccessfully()
    end Try

    return result
End Function

Function testDeserializationPP(testSuite as TestSuite, testObj as PersonProp)

    On Error GoTo logErr

    Call testSuite.describe("Test person first name").assertEqualsString("Paul", testObj.firstName, false)
    Call testSuite.describe("Test person last name set to surname").assertEqualsString("Withers", testObj.surname, false)
    Call testSuite.describe("Test full name").assertEqualsString("Paul Withers", testObj.getName(), false)
    Call testSuite.describe("Test pets size").assertEqualsInteger(1, UBound(testObj.pets))
    Call testSuite.describe("Test first pet type").assertIs("PetProp", testObj.pets(0))
    Call testSuite.describe("Test first pet name").assertEqualsString("Peter", testObj.pets(0).name, false)
    Call testSuite.describe("Test second pet name").assertEqualsString("Benjamin", testObj.pets(1).name, false)
    Call testSuite.describe("Test first pet type").assertEqualsString("Guinea Pig", testObj.pets(0).type, false)

getOut:
    Exit Function

logErr:
    Call testSuite.addError(|Error | & Error() & |(| & Err() & |) on line | & CStr(Erl), ||)
	Resume Next

End Function

Function testSerializationPP(testSuite as TestSuite, testObj as JsonObject)

    Call testSuite.describe("Test person first name").assertEqualsString("Paul", testObj.getChild("firstName").scalarValue, false)
    Call testSuite.describe("Test person last name").assertEqualsString("Withers", testObj.getChild("lastName").scalarValue, false)
    Call testSuite.describe("Test pet 1 name").assertEqualsString("Peter", testObj.getChild("animals").getChildren()(0).getChild("name").scalarValue, false)
    Call testSuite.describe("Test pet 1 type").assertEqualsString("Guinea Pig", testObj.getChild("animals").getChildren()(0).getChild("type").scalarValue, false)
    Call testSuite.describe("Test pet 2 name").assertEqualsString("Benjamin", testObj.getChild("animals").getChildren()(1).getChild("name").scalarValue, false)
    Call testSuite.describe("Test pet 2 type").assertEqualsString("Guinea Pig", testObj.getChild("animals").getChildren()(1).getChild("type").scalarValue, false)

End Function